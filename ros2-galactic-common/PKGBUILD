# Maintainer: Mohammad Mostafa Farzan <m2_farzan@yahoo.com>

pkgname=ros2-galactic-common
pkgver=2022_02_05_18_23
pkgrel=1
pkgdesc="Common ROS 2 Packages (See upstream for the list)"
url="https://github.com/m2-farzan/ros2-supplementary-pkgbuilds"
arch=('any')
conflicts=('ros2-galactic-xacro')
depends=(
    'ros2-galactic'
    'boost'
    'ceres-solver'
    'graphicsmagick'
    'lcov'
    'opencv'
    'python-pyzmq'
    'python-yaml'
    'python-yaml'
    'tbb'
)
source=(
    "fetch/angles::git+https://github.com/ros/angles#branch=galactic-devel"
    "fetch/behaviortree_cpp_v3::git+https://github.com/BehaviorTree/BehaviorTree.CPP"
    "fetch/bond_core::git+https://github.com/ros/bond_core#branch=ros2"
    "fetch/diagnostics::git+https://github.com/ros/diagnostics#branch=galactic"
    "fetch/geographic_info::git+https://github.com/ros-geographic-info/geographic_info#branch=ros2"
    "fetch/navigation2::git+https://github.com/ros-planning/navigation2#branch=galactic"
    "fetch/ompl::git+https://github.com/ompl/ompl"
    "fetch/robot_localization::git+https://github.com/cra-ros-pkg/robot_localization#branch=galactic"
    "fetch/slam_toolbox::git+https://github.com/SteveMacenski/slam_toolbox#branch=galactic"
    "fetch/xacro::git+https://github.com/ros/xacro#branch=ros2"

    "gazebo_ros_pkgs.repos::https://raw.githubusercontent.com/ros-simulation/gazebo_ros_pkgs/galactic/gazebo_ros_pkgs.repos"
    "gazebo_ros_pkgs.repos.patch::gazebo_ros_pkgs.repos.patch"
    "slam_toolbox.patch::slam_toolbox.patch"
    "turtlebot3.repos::https://raw.githubusercontent.com/ROBOTIS-GIT/turtlebot3/galactic-devel/turtlebot3.repos"
)
sha256sums=(
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'

    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
)

pkgver() {
    printf "$(date +'%Y_%m_%d_%H_%M')"
}

prepare() {
    # robot_localization
    sed -i "s/c++14/c++17/g" $srcdir/robot_localization/CMakeLists.txt

    # gazebo_ros_pkgs
    patch -p0 --forward $(readlink -f $srcdir/gazebo_ros_pkgs.repos) $srcdir/gazebo_ros_pkgs.repos.patch || true
    mkdir -p $srcdir/gazebo_ros_pkgs
    vcs import $srcdir/gazebo_ros_pkgs < $srcdir/gazebo_ros_pkgs.repos

    # slam_toolbox
    git -C $srcdir/slam_toolbox checkout fab61e1225d6c1ee5d634a4851b0a387c11c2c39
    git -C $srcdir/slam_toolbox apply $srcdir/slam_toolbox.patch

    # turtlebot3
    mkdir -p $srcdir/turtlebot3
    vcs import $srcdir/turtlebot3 < $srcdir/turtlebot3.repos
}

build() {
    # Disable parallel build if RAM is low
    if [[ $(free | grep -Po "Mem:\s+\K\d+") < 32000000 ]]; then
        printf "\nRAM is smaller than 32 GB. Parallel build will be disabled for stability.\n\n"
        export COLCON_EXTRA_ARGS="${COLCON_EXTRA_ARGS} --executor sequential"
    fi

    # Build
    colcon build --merge-install --packages-skip ${COLCON_EXTRA_ARGS}
}

package() {
    # Copy build files
    mkdir -p $pkgdir/opt/ros2/galactic-common
    cp -r $srcdir/install/* $pkgdir/opt/ros2/galactic-common/
}
