# Maintainer: Mohammad Mostafa Farzan <m2_farzan@yahoo.com>

pkgname=ros2-galactic-robot-localization
pkgver=r996.cda9107
pkgrel=1
pkgdesc="Slam Toolbox for lifelong mapping and localization in potentially massive maps with ROS 2"
url="https://navigation.ros.org/tutorials/docs/navigation2_with_slam.html"
arch=('any')
depends=('ros2-galactic'
         'ros2-galactic-navigation'
)
source=("robot_localization::git+https://github.com/cra-ros-pkg/robot_localization#branch=galactic"
        "git+https://github.com/ros-geographic-info/geographic_info#branch=ros2"
        "git+https://github.com/ros/diagnostics#branch=galactic"
)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
)

pkgver() {
    cd $srcdir/robot_localization
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    sed -i "s/c++14/c++17/g" $srcdir/robot_localization/CMakeLists.txt
}

build() {
    # Skip locally installed packages to avoid pacman conflicts
    PROVIDED_PACKAGES_=$(find $srcdir -name package.xml | xargs dirname | xargs -L1 basename | sort | uniq)
    INSTALLED_PACKAGES_=$(pacman -Ql ros2-galactic | grep -oP "/share/\K[^/]+" | sort | uniq)
    EXCLUDE_LIST_=$(comm -12 <(printf "${PROVIDED_PACKAGES_}") <(printf "${INSTALLED_PACKAGES_}"))

    # Build
    colcon build --merge-install --packages-skip ${EXCLUDE_LIST_}
}

package() {
    # Copy build files
    mkdir -p $pkgdir/opt/ros2/galactic
    cp -r $srcdir/install/* $pkgdir/opt/ros2/galactic/
    # Exclude files that clash with base ros installation
    rm $pkgdir/opt/ros2/galactic/*setup.*
    rm $pkgdir/opt/ros2/galactic/_local_setup*
    rm $pkgdir/opt/ros2/galactic/COLCON_IGNORE
}
