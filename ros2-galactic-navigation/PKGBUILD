# Maintainer: Mohammad Mostafa Farzan <m2_farzan@yahoo.com>

pkgname=ros2-galactic-navigation
pkgver=r2051.d8f50da2e
pkgrel=1
pkgdesc="The spiritual successor of the ROS Navigation Stack (AKA Nav2)"
url="https://navigation.ros.org/"
arch=('any')
depends=('ros2-galactic'
         'ros2-galactic-gazebo-ros-pkgs'
         'opencv'
         'python-pyzmq'
         'lcov'
         'boost-libs'
         'python-yaml'
         'ceres-solver'
         )
source=("navigation2::git+https://github.com/ros-planning/navigation2#branch=galactic"
        "bond_core::git+https://github.com/ros/bond_core#branch=ros2"
        "behaviortree_cpp_v3::git+https://github.com/BehaviorTree/BehaviorTree.CPP"
        "angles::git+https://github.com/ros/angles#branch=ros2"
        "ompl::git+https://github.com/ompl/ompl"
)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
)

pkgver() {
  cd $srcdir/navigation2
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

# prepare() {
# }

build() {
    # Skip locally installed packages to avoid pacman conflicts
    PROVIDED_PACKAGES_=$(find $srcdir/src -name package.xml | xargs dirname | xargs -L1 basename | sort | uniq)
    INSTALLED_PACKAGES_=$(pacman -Ql ros2-galactic | grep -oP "/share/\K[^/]+" | sort | uniq)
    EXCLUDE_LIST_=$(comm -12 <(printf "${PROVIDED_PACKAGES_}") <(printf "${INSTALLED_PACKAGES_}"))

    # Build
    colcon build --merge-install --packages-skip ${EXCLUDE_LIST_}
}

package() {
    # Copy build files
    mkdir -p $pkgdir/opt/ros2/galactic
    cp -r $srcdir/install/* $pkgdir/opt/ros2/galactic/
    # Exclude files that clash with base ros installation
    rm $pkgdir/opt/ros2/galactic/*setup.*
    rm $pkgdir/opt/ros2/galactic/_local_setup*
    rm $pkgdir/opt/ros2/galactic/COLCON_IGNORE
}
